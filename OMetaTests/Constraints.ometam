// Visual Constraint Syntax


ometa Constraints {
  null = empty -> {{{[NSNull null]}}},
  visualFormatString = (orientation ':' | null) : o
                       (superview connection | null) : ls
                       (view (connection view)*) : v
                       (connection superview | null) : rs -> {{{ [@[o,ls,v,rs] compact] }}},
  orientation = 'H' | 'V',
  superview = '|',
  view = '[' viewName:v (predicateListWithParens|null):p ']' -> {{{[@[@"view",v,p] compact]}}},
  connection = ('-' predicateList:p '-' -> {{{p}}}) | '-' | null,
  predicateList = simplePredicate | predicateListWithParens,
  simplePredicate = metricName | positiveNumber,
  predicateListWithParens = '(' predicate:p (',' predicate)* : ps ')' -> {{{ [@[p] arrayByAddingObjectsFromArray:ps] }}},
  predicate = (relation|null):r objectOfPredicate:o ('@' priority | null):p -> {{{ [@[r, o, p] compact] }}},
  relation = '=' '=' | '<' '=' | '>' '=',
  objectOfPredicate = constant | viewName,
  priority = metricName | number,
  constant = metricName | number,
  metricName = cIdentifier,
  viewName = cIdentifier,
  positiveNumber = number:n ? {{{[n intValue] > 0 }}} -> {{{ n }}},
  


  number = digit+ : ds -> {{{ @([[ds componentsJoinedByString:@""] integerValue])}}} ,
  cIdentifier = identifierStart:x identifierPart*:xs -> {{{[[@[x] arrayByAddingObjectsFromArray:xs] componentsJoinedByString:@""]}}},
  identifierStart = letter | '_',
  charRange :x :y = char:d ? {{{ [d characterAtIndex:0] >= [x characterAtIndex:0] && [d characterAtIndex:0] <= [y characterAtIndex:0] }}} -> {{{ d }}} ,
  digit = charRange({{{@"0"}}},{{{@"9"}}}),
  lower = charRange({{{@"a"}}},{{{@"z"}}}),
  upper = charRange({{{@"A"}}},{{{@"Z"}}}),
  letter = lower | upper,
  identifierPart = identifierStart | digit
}
